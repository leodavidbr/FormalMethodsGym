IMPLEMENTATION Gym_i
REFINES Gym
    
CONSTANTS
    r_maximumClient,
    r_maximumWorkout,
    r_maximumExercise,
    r_maximumCategory
    
PROPERTIES
    r_maximumClient = SZE_CLIENT - 1
    & r_maximumWorkout = SZE_WORKOUT - 1
    & r_maximumExercise = SZE_EXERCISE -1
    & r_maximumCategory = SZE_CATEGORY -1
    
    & r_maximumClient : CLIENT
    & r_maximumWorkout : WORKOUT
    & r_maximumExercise : EXERCISE
    & r_maximumCategory : CATEGORY
    
CONCRETE_VARIABLES
    
    r_clients
    ,r_workouts
    ,r_exercises
    
    , r_clientHasWorkout
    ,r_clientWorkedOut
    ,r_workoutHasExercise
    ,r_exerciseHasCategory
    ,r_clientHasSolicitation
    ,r_workoutHasCategory
    ,r_isClientNew
    
    
    
INVARIANT
    
    // --- CONJUNTOS --- //
    r_clients : CLIENT --> BOOL 
    & clients = dom(r_clients |> {TRUE}) 
    &
    r_workouts : WORKOUT --> BOOL
    & workouts = dom(r_workouts |> {TRUE})
    &
    r_exercises: EXERCISE --> BOOL 
    & exercises = dom(r_exercises |> {TRUE})
    &
    
    // --- FUNCOES --- ///
    
    r_exerciseHasCategory : EXERCISE --> CATEGORY 
    & !ex. ((ex : EXERCISE & ex : exercises) => 
        (r_exerciseHasCategory(ex) = exerciseHasCategory(ex)))
    &
    r_clientHasSolicitation : CLIENT --> BOOL
    & !cl. ((cl : CLIENT & cl : clients) => 
        (r_clientHasSolicitation(cl) = clientHasSolicitation(cl)))
    &
    r_workoutHasCategory : WORKOUT --> CATEGORY
    & !wr. ((wr : WORKOUT & wr : workouts) => 
        (r_workoutHasCategory(wr) = workoutHasCategory(wr)))
    &
    r_isClientNew : CLIENT --> BOOL
    & !cl. ((cl : CLIENT & cl : clients) 
        => (r_isClientNew(cl) = isClientNew(cl)))
    
    // --- RELACOES --- //
    
    & 
    r_clientHasWorkout : CLIENT * WORKOUT --> BOOL
    & !(cl) .( (cl : CLIENT & cl : clients) 
        => (clientHasWorkout[{cl}] =
            UNION(wr).(wr : WORKOUT & wr : workouts & r_clientHasWorkout(cl,wr) = TRUE | {wr}) ) )
    
    //    & 
    //    r_clientHasWorkout : CLIENT * WORKOUT --> BOOL
    //    & !(cl, wr) .( (cl : CLIENT & wr : WORKOUT & cl : clients & wr : workouts) 
    //        => (wr : clientHasWorkout[{cl}] <=> (r_clientHasWorkout(cl,wr) = TRUE)) )
    
    & 
    r_clientWorkedOut : CLIENT * WORKOUT --> BOOL 
    & !(cl) .( (cl : CLIENT & cl : clients) 
        => (clientWorkedOut[{cl}] =
            UNION(wr).(wr : WORKOUT & r_clientWorkedOut(cl,wr) = TRUE | {wr}) ) )
    
    & 
    r_workoutHasExercise : WORKOUT * EXERCISE --> BOOL 
    & !(wr) .( (wr : WORKOUT & wr : workouts) 
        => ( workoutHasExercise[{wr}] =
            UNION(ex).(ex : EXERCISE & ex : exercises & r_workoutHasExercise(wr, ex) = TRUE | {ex}) ) )
    
    // Outras formas de ligar as relacoes que pensei:
    //     Forma 01
    //        r_clientWorkedOut : CLIENT * WORKOUT --> BOOL 
    //        & !(cl, wr) .( (cl : CLIENT & wr : WORKOUT & cl : clients) 
    //            => ( wr : clientWorkedOut[{cl}] <=> (r_clientWorkedOut(cl,wr) = TRUE) ) )
    //
    // Forma 02
    //    r_clientWorkedOut : CLIENT * WORKOUT --> BOOL 
    //    & !(cl) .( (cl : CLIENT & cl : clients) 
    //        => (clientWorkedOut[{cl}] = (ran(dom(r_clientWorkedOut |> {TRUE})) ) ) ) 
    //
    // Forma 03
    //    r_clientWorkedOut : CLIENT * WORKOUT --> BOOL 
    //       & clientWorkedOut = dom( (UNION(cl, wr).(cl : CLIENT & cl : clients & wr : WORKOUT | {cl |-> wr} )) <| (r_clientWorkedOut |> {TRUE}))
    
VALUES
    maxClients = 15;
    maxWorkouts = 15;
    maxExercises = 15;
    
    maxExercisesInWorkout = 7;
    //    maxWorkoutsInHistory = 40;
    exactNumClientWorkouts = 3;
    
    SZE_CLIENT = 15;
    SZE_WORKOUT = 15;
    SZE_EXERCISE = 15;
    SZE_CATEGORY = 3;
    
    r_maximumClient = SZE_CLIENT - 1;
    r_maximumWorkout = SZE_WORKOUT - 1;
    r_maximumExercise = SZE_EXERCISE - 1;
    r_maximumCategory = SZE_CATEGORY - 1;
    
    
    CLIENT = 0..r_maximumClient;
    WORKOUT = 0..r_maximumWorkout;
    EXERCISE = 0..r_maximumExercise; 
    CATEGORY = 0..r_maximumCategory;
    
    
    PUSH = 0;
    PULL = 1;
    LEGS = 2; 
    
    NULL = 0;
    ANYEXERCISE = 0;
    ANYWORKOUT = 0;
    ANYCATEGORY = 0
    
    
INITIALISATION
    r_clients := CLIENT*{FALSE};
    r_workouts := WORKOUT*{FALSE};
    r_exercises := EXERCISE*{FALSE};
    r_exerciseHasCategory := EXERCISE*{ANYCATEGORY};
    r_clientHasSolicitation := CLIENT*{FALSE};
    r_workoutHasCategory := WORKOUT*{ANYCATEGORY};
    r_isClientNew := CLIENT*{TRUE};
    r_clientHasWorkout := (CLIENT*WORKOUT)*{FALSE};
    r_clientWorkedOut := (CLIENT*WORKOUT)*{FALSE};
    r_workoutHasExercise := (WORKOUT*EXERCISE)*{FALSE}
    
    
OPERATIONS
    
    // OK (acho)
    addClient(client) = 
    BEGIN        
        // !workoutx r_clientHasWorkout(client*workoutx) = FALSE
        // Na pratica so precisa de !workoutx : workouts, mas assim eh mais facil e da certo
        VAR ww IN
            ww := 0;
            WHILE ww <= r_maximumWorkout DO
                r_clientHasWorkout(client, ww) := FALSE;
                ww := ww + 1
            INVARIANT
                ww : 0..(r_maximumWorkout +1)
                & r_clientHasWorkout : CLIENT * WORKOUT --> BOOL
                & !workoutx. (workoutx : 0..(ww-1) 
                    => r_clientHasWorkout(client, workoutx) = FALSE)
            VARIANT
                SZE_WORKOUT - ww +2
            END
        END;
        
        //!workoutx r_clientWorkedOut(client*workoutx) := FALSE 
        VAR ww IN
            ww := 0;
            WHILE ww <= r_maximumWorkout DO
                r_clientWorkedOut(client, ww) := FALSE;
                ww := ww + 1
            INVARIANT
                r_clientWorkedOut : CLIENT * WORKOUT --> BOOL
                & ww : 0..(r_maximumWorkout+1)
                & !workoutx. (workoutx : 0..(ww-1) 
                    => r_clientWorkedOut(client, workoutx) = FALSE)
            VARIANT
                SZE_WORKOUT - ww +2
            END
        END;
        r_clients(client) := TRUE;
        r_clientHasSolicitation(client) := TRUE;
        r_isClientNew(client) := TRUE
        
    END;
    
    // Como nas invariantes de ligacao somente nos importamos com client que pertenca a clients nos relacionamentos e funcoes (que na maquina o dominio/contradominio sao clients), quando o client eh removido de r_clients tudo da certo. Mas em compensacao precisamos inicializar corretamente ao cadastrar um client
    // Provas ok (eu acho)
    removeClient(client) = 
    BEGIN
        r_clients(client) := FALSE
    END;
    
    // TODO: r_workoutHasExercise
    addWorkout(workout, categoryWr, exercisesInWorkout) = 
    BEGIN
        r_workouts(workout) := TRUE;
        r_workoutHasCategory(workout) := categoryWr;
        // !clientx r_clientHasWorkout(clientx*workout) = FALSE
        VAR cc IN
            cc := 0;
            WHILE cc <= r_maximumClient DO
                r_clientHasWorkout(cc, workout) := FALSE;
                cc := cc + 1
            INVARIANT
                cc : 0..(r_maximumClient +1)
                & r_clientHasWorkout : CLIENT * WORKOUT --> BOOL
                & !clientx. (clientx : 0..(cc-1) 
                    => r_clientHasWorkout(clientx, workout) = FALSE)
            VARIANT
                SZE_WORKOUT - cc +2
            END
        END;
        // !exercisex r_workoutHasExercise(workout, exercisex) := exercisesInWorkout(exercisex);
        VAR ex IN
            ex := 0;
            WHILE ex <= r_maximumExercise DO
                r_workoutHasExercise(workout, ex) := exercisesInWorkout(ex);
                ex := ex +1
            INVARIANT
                ex : 0..(r_maximumExercise +1)
                & r_workoutHasExercise : WORKOUT * EXERCISE --> BOOL
                & exercisesInWorkout : NAT --> BOOL
                & dom(exercisesInWorkout |> {TRUE}) <: EXERCISE 
                & !exercisex. (exercisex : 0..(ex-1) 
                    => r_workoutHasExercise(workout, exercisex) = exercisesInWorkout(exercisex))
            VARIANT
                r_maximumExercise - ex + 2
            END
        END
    END;
    
    // PROVAS OK (acho)
    removeWorkout(workout) = 
    BEGIN
        r_workouts(workout) := FALSE
    END;
    
    // PROVAS OK (acho)
    addExercise(exercise, categoryEx) = 
    BEGIN
        r_exercises(exercise) := TRUE;
        r_exerciseHasCategory(exercise) := categoryEx;
        
        // !workoutx r_workoutHasExercise(workoutx,exercise) = FALSE
        VAR ww IN
            ww := 0;
            WHILE ww <= r_maximumWorkout DO
                r_workoutHasExercise(ww, exercise) := FALSE;
                ww := ww + 1
            INVARIANT
                ww : 0..(r_maximumWorkout +1)
                & r_workoutHasExercise : WORKOUT * EXERCISE --> BOOL
                & !workoutx. (workoutx : 0..(ww-1) 
                    => r_workoutHasExercise(workoutx, exercise) = FALSE)
            VARIANT
                SZE_WORKOUT - ww +2
            END
        END
    END;
    
    // PROVAS OK (acho)
    removeExercise(exercise) = 
    BEGIN
        r_exercises(exercise) := FALSE
    END;
    
    // PROVAS OK (acho)
    askForWorkout(client) = 
    BEGIN
        r_clientHasSolicitation(client) := TRUE
    END;
    
    workoutLog <-- getWorkoutLog(client) = 
    BEGIN
        // !workoutx log(workoutx) <=> r_clientWorkedOut(client, workoutx)
        VAR ww IN
            ww := 0;
            WHILE ww <= r_maximumWorkout DO
                workoutLog(ww) := r_clientWorkedOut(client, ww);
                ww := ww + 1
            INVARIANT
                ww : NAT
                & ww : 0..(r_maximumWorkout +1)
                & r_clientWorkedOut : CLIENT * WORKOUT --> BOOL
                & workoutLog : NAT --> BOOL
                & dom(workoutLog |> {TRUE}) <: WORKOUT
                & !workoutx. (workoutx : 0..(ww-1) 
                    => workoutLog(workoutx) = r_clientWorkedOut(client, workoutx))
            VARIANT
                r_maximumWorkout - ww +2
            END
        END
    END;
    
    clientsWithSolicitation <-- getClientsWithWorkoutSolicitation = 
    BEGIN
        // !clientx clientsWithSolicitation(clientx) = r_clientHasSolicitation(clientx)
        VAR ww IN
            ww := 0;
            WHILE ww <= r_maximumClient DO
                clientsWithSolicitation(ww) := r_clientHasSolicitation(ww);
                ww := ww + 1
            INVARIANT
                ww : 0..(r_maximumClient +1)
                & r_clientHasSolicitation : CLIENT --> BOOL
                & clientsWithSolicitation : NAT --> BOOL
                & dom(clientsWithSolicitation |> {TRUE}) <: CLIENT
                & !clientx. (clientx : 0..(ww-1) 
                    => clientsWithSolicitation(clientx) = r_clientHasSolicitation(clientx))
            VARIANT
                r_maximumClient - ww +2
            END
        END
    END;
    
    // TODO: clientWorkedOut
    defineClientWorkouts(client, wr1, wr2, wr3) = 
    BEGIN
        r_clientHasSolicitation(client) := FALSE;
        r_isClientNew(client) := FALSE;
        
        // !workoutx IF r_clientHasWorkout(client,workoutx) THEN r_clientWorkedOut(client, workoutx)
        VAR ww IN
            ww := 0;
            WHILE ww <= r_maximumWorkout DO
                IF ( r_clientHasWorkout(client, ww) = TRUE ) THEN
                    r_clientWorkedOut(client, ww) := TRUE
                END;
                ww := ww + 1
            INVARIANT
                ww : NAT
                & ww : 0..(r_maximumWorkout +1)
                & r_clientWorkedOut : CLIENT * WORKOUT --> BOOL
                & r_clientHasWorkout : CLIENT * WORKOUT --> BOOL
                & !workoutx. (workoutx : 0..(ww-1) 
                    => (r_clientHasWorkout(client,workoutx) = TRUE => r_clientWorkedOut(client, workoutx) = TRUE) )
            VARIANT
                r_maximumWorkout - ww +2
            END
        END;
        r_clientHasWorkout(client, wr1) := TRUE;
        r_clientHasWorkout(client, wr2) := TRUE;
        r_clientHasWorkout(client, wr3) := TRUE
    END
    
END
// add client ok
// remove client ok
// remove workout ok
// add exercise ok
// remove exercise ok
// askForWorkout ok
// getWorkoutLog ok
// add workout ok
// getClientsWithWorkoutSolicitations ok
// defineClientWorkouts ok